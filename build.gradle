/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2018-2025 Agorapulse.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'org.kordamp.gradle.groovy-project'
    id 'org.kordamp.gradle.checkstyle'
    id 'org.kordamp.gradle.codenarc'
    id 'org.kordamp.gradle.coveralls'
}

if (!project.hasProperty('mavenCentralUsername'))       ext.mavenCentralUsername            = System.getenv('MAVEN_CENTRAL_USERNAME') ?: '**UNDEFINED**'
if (!project.hasProperty('mavenCentralPassword'))       ext.mavenCentralPassword            = System.getenv('MAVEN_CENTRAL_PASSWORD') ?: '**UNDEFINED**'
if (!project.hasProperty('signingInMemoryKeyId'))       ext.signingInMemoryKeyId            = System.getenv('SIGNING_KEY_ID') ?: '**UNDEFINED**'
if (!project.hasProperty('signingInMemoryKeyPassword')) ext.signingInMemoryKeyPassword      = System.getenv('SIGNING_PASSWORD') ?: '**UNDEFINED**'
if (!project.hasProperty('signingInMemoryKey'))         ext.signingInMemoryKey              = System.getenv('SIGNING_SECRET_KEY_PATH') ? rootProject.file(System.getenv('SIGNING_SECRET_KEY_PATH')).text : '**UNDEFINED**'


config {
    release = (rootProject.findProperty('release') ?: false).toBoolean()

    info {
        name        = 'Micronaut Libraries'
        vendor      = 'Agorapulse'
        description = 'Set of useful libraries for Micronaut'

        links {
            website      = "https://github.com/${slug}"
            issueTracker = "https://github.com/${slug}/issues"
            scm          = "https://github.com/${slug}.git"
        }

        people {
            person {
                id    = 'musketyr'
                name  = 'Vladimir Orany'
                roles = ['developer']
            }
        }

        repositories {
            repository {
                name = 'localRelease'
                url  = "${project.rootProject.buildDir}/repos/local/release"
            }
            repository {
                name = 'localSnapshot'
                url  = "${project.rootProject.buildDir}/repos/local/snapshot"
            }
        }
    }

    licensing {
        licenses {
            license {
                id = 'Apache-2.0'
            }
        }
    }

    publishing {
        enabled = false
        signing {
            enabled = false
        }
    }

    artifacts {
        source {
            enabled = false
        }
    }

    quality {
        checkstyle {
            toolVersion = '8.27'
        }

        codenarc {
            toolVersion = '1.5'
        }
    }

    docs {
        javadoc {
            enabled = false
            autoLinks {
                enabled = false
            }
            aggregate {
                enabled = false
            }
        }
        groovydoc {
            enabled = false
            aggregate {
                enabled = false
            }
        }
    }

}


allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/release' }
    }

    license {
        exclude '**/*.json'
        exclude '***.yml'
        exclude 'logback.xml'
    }
}

gradleProjects {
    subprojects {
        dirs(['subprojects', 'platforms']) {
            mavenPublishing {
                publishToMavenCentral(true)
                signAllPublications()

                pom {
                    name = "Micronaut AWS SDK"
                    description = 'Set of useful libraries for Micronaut'
                    inceptionYear = "2018"
                    url = "https://github.com/${slug}"
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                            distribution = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    developers {
                        developer {
                            id = "musketyr"
                            name = "Vladimir Orany"
                            url = "https://github.com/musketyr/"
                        }
                    }
                    scm {
                        url = "https://github.com/${slug}.git"
                        connection = "scm:git:git://github.com/${slug}.git"
                        developerConnection = "scm:git:ssh://git@github.com/${slug}.git"
                    }
                }
            }

            signing {
                useInMemoryPgpKeys(rootProject.ext.signingInMemoryKey, rootProject.ext.signingInMemoryKeyPassword)
                sign publishing.publications
            }
        }

        dirs(['subprojects', 'benchmarks']) { Project subproject ->
            micronaut {
                if (it.hasProperty('importMicronautPlatform')) {
                    importMicronautPlatform = true
                } else {
                    project.logger.lifecycle "No importMicronautPlatform property found for ${micronaut.dump()}"
                }
                testRuntime 'spock'
                processing {
                    incremental false
                }
            }

            dependencies {
                api platform(project(':micronaut-aws-sdk-dependencies'))

                // Groovy
                if (subproject.name.startsWith('micronaut-amazon-awssdk')) {
                    compileOnly "org.apache.groovy:groovy:$groovyVersion"
                    compileOnly 'io.micronaut.aws:micronaut-aws-sdk-v2'
                    testImplementation "org.apache.groovy:groovy:$groovyVersion"
                    testImplementation 'io.micronaut.aws:micronaut-aws-sdk-v2'
                } else {
                    compileOnly 'io.micronaut.aws:micronaut-aws-sdk-v1'
                    implementation "org.apache.groovy:groovy:$groovyVersion"
                    implementation 'io.micronaut.groovy:micronaut-runtime-groovy'
                    compileOnly 'io.micronaut:micronaut-inject-groovy'
                    testImplementation 'io.micronaut.aws:micronaut-aws-sdk-v1'
                }

                implementation 'io.micronaut:micronaut-inject'
                implementation 'io.micronaut:micronaut-runtime'


                testAnnotationProcessor 'io.micronaut:micronaut-inject-java'

                testImplementation 'io.micronaut:micronaut-inject-groovy'

                testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
                testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.1'


                testImplementation 'io.micronaut.test:micronaut-test-junit5'
                testImplementation 'io.micronaut.test:micronaut-test-spock'

                testRuntimeOnly 'ch.qos.logback:logback-classic:1.2.3'
                testRuntimeOnly 'org.yaml:snakeyaml'
            }

            compileJava.options.compilerArgs += '-parameters'
            compileTestJava.options.compilerArgs += '-parameters'

            test {
                useJUnitPlatform()
                environment 'AWS_CBOR_DISABLE', 'true'
                systemProperty 'TEST_RESOURCES_FOLDER', new File(subproject.projectDir, 'src/test/resources').canonicalPath
                systemProperty 'user.timezone', 'UTC'
                systemProperty 'user.language', 'en'
            }

            task cleanOut(type: Delete) {
                delete file('out')
            }

            clean.dependsOn cleanOut
        }

        dirs(['subprojects', 'kotlin-libs']) { Project subproject ->
            project(':micronaut-aws-sdk-bom').dependencies.constraints.api subproject
        }
    }
}

check.dependsOn('aggregateCheckstyle', 'aggregateCodenarc', 'aggregateAllTestReports', 'coveralls')
