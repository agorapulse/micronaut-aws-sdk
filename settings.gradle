/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2018-2022 Agorapulse.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
pluginManagement {
    repositories {
        gradlePluginPortal()
    }
    plugins {
        id 'org.kordamp.gradle.groovy-project' version kordampVersion
        id 'org.kordamp.gradle.checkstyle'     version kordampVersion
        id 'org.kordamp.gradle.codenarc'       version kordampVersion
        id 'org.kordamp.gradle.guide'          version kordampVersion
        id 'org.kordamp.gradle.coveralls'      version kordampVersion
        id 'com.github.kt3k.coveralls'         version '2.9.0'
        id 'org.ajoberstar.git-publish'        version '2.1.3'
    }
}

buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath "org.kordamp.gradle:settings-gradle-plugin:$kordampVersion"
        // forces the version suitable for all currently used plugins
        classpath 'org.eclipse.jgit:org.eclipse.jgit:5.6.0.201912101111-r', {
            force = true
        }
        classpath 'org.ajoberstar.grgit:grgit-core:4.0.1'
        classpath "io.github.gradle-nexus:publish-plugin:1.0.0"
    }
}


plugins {
    id 'com.gradle.enterprise' version '3.6.1'
}

gradleEnterprise {
    buildScan {
        termsOfServiceUrl = "https://gradle.com/terms-of-service"
        termsOfServiceAgree = "yes"

        buildScanPublished { scan ->
            file("scan-journal.log") << "${new Date()} - ${scan.buildScanId} - ${scan.buildScanUri}\n"
        }
    }
}


apply plugin: 'org.kordamp.gradle.settings'

projects {
    directories = ['subprojects', 'examples', 'docs', 'benchmarks']
}

rootProject.name = 'micronaut-aws-sdk'
